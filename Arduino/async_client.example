static AsyncClient * aClient = NULL;

void runAsyncClient(){
  if(aClient)//client already exists
    return;

  aClient = new AsyncClient();
  if(!aClient)//could not allocate client
    return;

  aClient->onError([](void * arg, AsyncClient * client, int error){
    Serial.println("Connect Error");
    aClient = NULL;
    delete client;
  }, NULL);

  aClient->onConnect([](void * arg, AsyncClient * client){
    Serial.println("Connected");
    aClient->onError(NULL, NULL);

    client->onDisconnect([](void * arg, AsyncClient * c){
      Serial.println("Disconnected");
      aClient = NULL;
      delete c;
    }, NULL);

    client->onData([](void * arg, AsyncClient * c, void * data, size_t len){
      Serial.print("\r\nData: ");
      Serial.println(len);
      uint8_t * d = (uint8_t*)data;
      for(size_t i=0; i<len;i++)
        Serial.write(d[i]);
    }, NULL);

    //send the request
    client->write("GET / HTTP/1.0\r\nHost: www.google.com\r\n\r\n");
  }, NULL);

  if(!aClient->connect("www.google.com", 80)){
    Serial.println("Connect Fail");
    AsyncClient * client = aClient;
    aClient = NULL;
    delete client;
  }
}

================================================================================
void setup()
{
	Serial.begin(115200);
	Serial.println();
	Serial.setDebugOutput(true);
	WiFi.mode(WIFI_STA);
	WiFi.config(local_ip, gateway_ip, subnet_ip);
	WiFi.begin(ssid, password);
        runAsyncClient();
}

What's wrong with my code? :)
UPD:
I don't really understand what is going on but my fix helped me:

WiFi.begin(ssid, password);
while ( WiFi.status() != WL_CONNECTED )
{
   delay ( 500 );
   Serial.print ( "." );
}
runAsyncClient();
================================================================================
static AsyncClient * client = NULL;

void someMethodThatCreatesClient(){
  //.....
  client = createdAsyncClient;
  //......
}

void onThatClientDisconnect(){
  client = NULL;
}

void loop(){
  if(client){
    Serial.printf("ClientState: %u", client->state());
  }
}